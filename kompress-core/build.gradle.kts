/*
 * Copyright 2026 Karma Krafts
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

@file:OptIn(ExperimentalKotlinGradlePluginApi::class)

import dev.karmakrafts.conventions.configureJava
import dev.karmakrafts.conventions.dokka.configureDokka
import dev.karmakrafts.conventions.kotlin.defaultCompilerOptions
import dev.karmakrafts.conventions.kotlin.withAndroidLibrary
import dev.karmakrafts.conventions.kotlin.withBrowser
import dev.karmakrafts.conventions.kotlin.withJvm
import dev.karmakrafts.conventions.kotlin.withNative
import dev.karmakrafts.conventions.kotlin.withNodeJs
import dev.karmakrafts.conventions.kotlin.withWasmWasi
import dev.karmakrafts.conventions.kotlin.withWeb
import dev.karmakrafts.conventions.setProjectInfo
import org.jetbrains.kotlin.gradle.ExperimentalKotlinGradlePluginApi
import org.jetbrains.kotlin.gradle.plugin.mpp.NativeBuildType
import org.jetbrains.kotlin.gradle.targets.jvm.tasks.KotlinJvmTest
import java.time.Duration

plugins {
    alias(libs.plugins.kotlin.multiplatform)
    alias(libs.plugins.android.library)
    alias(libs.plugins.dokka)
    signing
    `maven-publish`
}

configureJava(libs.versions.javaCompile, libs.versions.javaTarget)

configureDokka {
    withKotlin()
    withKotlinxIo()
}

kotlin {
    compilerOptions {
        freeCompilerArgs.add("-Xexpect-actual-classes")
    }
    defaultCompilerOptions()
    withSourcesJar()
    withAndroidLibrary("$group.core")
    withNative {
        binaries {
            test(listOf(NativeBuildType.RELEASE))
        }
    }
    // Отключено для JitPack-сборки — jdk.incubator.vector недоступен компилятору в этом окружении,
    // а consumer'у (photo-archiver) JVM-таргет и не нужен (Android/iOS/wasmJs).
    // withJvm {
    //     testRuns {
    //         create("vector") { // Run tests with jdk.incubator.vector API
    //             setExecutionSourceFrom(compilations["test"])
    //             executionTask {
    //                 modularity.inferModulePath = true
    //                 jvmArgs("--add-modules", "jdk.incubator.vector")
    //             }
    //         }
    //     }
    // }
    withWeb {
        withBrowser {
            useEsModules()
            testTask {
                timeout = Duration.ofMinutes(5)
                useKarma {
                    useFirefoxHeadless()
                    useConfigDirectory(rootProject.projectDir.resolve("karma.config.d"))
                }
            }
        }
        withNodeJs {
            testTask {
                timeout = Duration.ofMinutes(5)
                useMocha {
                    timeout = "300000"
                }
            }
        }
    }
    withWasmWasi {
        withNodeJs()
    }
    applyDefaultHierarchyTemplate {
        common {
            group("jvmAndAndroid") {
                // withJvm() // отключено для JitPack, см. выше
                withAndroidLibrary()
            }
            group("jsAndWasm") {
                withJs()
                withWasmJs()
                withWasmWasi()
            }
        }
    }
    sourceSets {
        commonMain {
            dependencies {
                api(libs.kotlinx.io.bytestring)
                api(libs.kotlinx.io.core)
                api(libs.kotlinx.datetime)
                implementation(libs.karbide.core)
            }
        }
        // jvmMain { ... } // отключено для JitPack, см. выше
        webMain {
            dependencies {
                implementation(libs.kotlin.wrappers.browser)
            }
        }
        commonTest {
            dependencies {
                implementation(libs.kotlin.test)
            }
        }
        // jvmTest { ... } // отключено для JitPack, см. выше
    }
}

tasks {
    withType<KotlinJvmTest>().configureEach {
        jvmArgs("-Xms2G", "-Xmx2G")
    }
}

publishing {
    setProjectInfo(
        name = "Kompress Core",
        description = "Lightweight compression API for Kotlin Multiplatform",
        url = "https://git.karmakrafts.dev/kk/kompress"
    )
}